###
###
###
@content[][]

^switch[$uri:path_first]{
	^case[admin]{^use[admin.p]^admin:main[]}
	^case[DEFAULT]{
		$MAIN:menu_id(^uri:inspect_uri[])
		^if($MAIN:menu_id > 0){
			^get_content_by_menu_id[$MAIN:menu_id]
		}{
			^if(^uri:path.count[] eq "2"){
				$MAIN:menu[^table::sql{select id, menu_id, menu_title, menu_uri, menu_parent_id, visible, module from ${MAIN:sql_config.table_prefix}menu where menu_uri = '$uri:path.piece' and menu_parent_id = '0'}[$.limit(1) $.offset(0)]]
				^if($MAIN:menu){
					^if(def $MAIN:menu.module && ^uri:path_last.pos[.html] > 0){
						^get_content_by_menu_id[$MAIN:menu.id]
					}{ ^404[] }
				}{ ^404[] }
			}{ ^404[] }
		}
	}
}

###
###
###
@get_content_by_menu_id[menu_id][]
^if(def $menu_id){
	$MAIN:menu_id[$menu_id]
	$MAIN:current_section[^table::sql{select id, menu_id, menu_title, menu_uri, menu_parent_id, visible, module from ${MAIN:sql_config.table_prefix}menu where id = '$MAIN:menu_id'}]
	$MAIN:current_document[^table::sql{select id, menu_id, title, keywords, description, h1, body, published, updated from ${MAIN:sql_config.table_prefix}content where menu_id = '$MAIN:menu_id'}]

	^if(-f "/cms/classes/modules/$MAIN:current_section.module"){
	$module_[
		$module_name[$MAIN:current_section.module]
		^use[$module_name]
		^process{^^^module_name.left(^module_name.pos[.]):index[]}
		^if($MAIN:user_status eq "99"){
			<div class="admin_link"><input type="button" value="Управление модулем" onClick="show_modal('${MAIN:language_prefix}/admin/module/?id=$module_name')" /></div>
			<br /><br /><br />
		}
	]
	}

	^if($MAIN:hide_index_text ne "1"){
		^if($MAIN:current_document){
			<h1>$MAIN:current_document.h1</h1>
			$MAIN:meta_title[^if(def $MAIN:current_document.title){$MAIN:current_document.title}{$MAIN:current_document.h1}]
			^untaint{$MAIN:current_document.body}
		}{
			<h1>$MAIN:current_section.menu_title</h1>
			$MAIN:meta_title[$MAIN:current_section.menu_title]
		}
		^if($MAIN:user_status eq "99"){
			<div class="admin_link"><input type="button" value="Изменить текст" onClick="show_modal('${MAIN:language_prefix}/admin/edit_content/?id=^MAIN:menu_id.int(0)')" /></div>
			<br /><br /><br />
		}
	}
	$module_

}


###
###
###
@404[][]
#$response:status(404)
<h1>Страница не найдена</h1>
<p />Документ <a href="${request:uri}">http://${env:HTTP_HOST}${request:uri}</a> не найден.
<p />Причины, которые могли привести к ошибке:
<ul>
<li /><b>Неправильно набран адрес</b><br />проверьте правильность адреса документа
<li /><b>Такой страницы никогда не было на этом сайте</b><br />зайдите на главную страницу или воспользуйтесь поиском
<li /><b>Такая страница была, но по этому адресу ее больше нет</b><br />пожалуйста, сообщите об этом <a href="mailto:info@gcschina.ru">администратору</a>
</ul>
